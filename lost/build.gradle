buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.2.3'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    classpath 'com.github.dcendents:android-maven-plugin:1.2'
  }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'checkstyle'

group = GROUP
version = VERSION_NAME

allprojects {
  repositories {
    mavenLocal()
    jcenter()
  }
}

android {
  compileSdkVersion 21
  buildToolsVersion "21.0.2"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 21
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
  }
}

repositories {
  jcenter()
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "started", "skipped", "passed", "failed"
    showStandardStreams true
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  include '**/*.kt'
  exclude '**/gen/**'

  classpath = files()
}

dependencies {
  compile 'com.android.support:appcompat-v7:22.2.0'
  compile 'com.android.support:support-v4:22.2.0'
  compile 'com.google.guava:guava:18.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.robolectric:robolectric:3.0-rc3'
  testCompile 'com.squareup:fest-android:1.0.7'
}

apply from: file('../gradle-mvn-push.gradle')
